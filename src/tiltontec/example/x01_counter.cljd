(ns tiltontec.example.x01-counter
  (:require
    [tiltontec.matrix.api :refer [cF cI mget  mswap! fm*] :as md]
    [tiltontec.flutter-mx.core :as fx
     :refer [scaffold app-bar text center column hero material-app sized-box]]
    ["package:flutter/widgets.dart" :as w]
    ["package:flutter/material.dart" :as m]
    ["package:flutter/painting.dart" :as p]))

(defn make-app []
  (material-app
    {:title "Flutter/MX Counter Demo"
     :theme (m/ThemeData .primarySwatch m.Colors/blue)}
    (scaffold
      {:appBar (app-bar
                 {:title (m/Text "Welcome to Flutter/MX World")})
       :floatingActionButton
       (cF (fx/floating-action-button
             {:onPressed (fx/as-dart-callback []
                           (mswap! (fm* :z-counter) :value inc))
              :tooltip   "Increment"}
             (m/Icon m.Icons/add .color m.Colors/black)))}
      (center
        (column
          {:mainAxisAlignment m.MainAxisAlignment/center}
          (text {:style (p/TextStyle .color m.Colors/black
                             .fontSize 18.0)}
            "You clicked the button this many times:")
          (sized-box {:height 14.0})
          (text
            {:style (fx/in-my-context [me ctx]
                      (.-headline4 (.-textTheme (m.Theme/of ctx))))}
            {:name  :z-counter
             :value (cI 0)}
            (str (md/my-value))))))))




